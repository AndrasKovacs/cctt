
inductive Top := tt;
inductive Bot :=;
not (A : U) := A → Bot;

inductive Sum (A B : U) := left A | right B;

Dec (A : U) := Sum A (not A);

--------------------------------------------------------------------------------

isLogEquiv (A B : U)(f : A → B) : U := (B → A);

isSection (A B : U)(f : A → B) : U := (g : B → A) × (linv : (x : A) → x = g (f x));

isRetraction (A B : U)(f : A → B) : U := (g : B → A) × (rinv : (x : B) → f (g x) = x);

isIso (A B : U)(f : A → B) : U :=
    (g    : B → A)
  × (linv : (x : A) → x = g (f x))
  × (rinv : (x : B) → f (g x) = x);

isEquiv (A B : U)(f : A → B) : U :=
    (g    : B → A)
  × (linv : (x : A) → x = g (f x))
  × (rinv : (x : B) → f (g x) = x)
  × (coh  : (x : A) → refl ={i. f (linv x i) = f x} (rinv (f x)));

coeIsEquiv (A : I → U)(r r' : I) : isEquiv (A r) (A r') (λ x. coe r r' A x) :=
  let f'    (i : I)(x : A r)        : A i := coe r i A x;
  let g'    (i : I)(x : A i)        : A r := coe i r A x;
  let linv' (i : I)(x : A r)(j : I) : A r := hcom r i (A r) [j=0 k. x; j=1 k. coe k r A (coe r k A x)] x;
  let rinv' (i : I)(x : A i)(j : I) : A i := hcom i r (A i) [j=0 k. coe k i A (coe i k A x); j=1 k. x] x;

  ( λ x. g' r' x
  , λ x j. linv' r' x j
  , λ x j. rinv' r' x j
  , λ x l k. com r r' A
                      [k=0 i. f' i (linv' i x l)
                      ;k=1 i. f' i x
		      ;l=0 i. f' i x
		      ;l=1 i. rinv' i (f' i x) k]
		      x
  );

equiv (A B : U) : U :=
  (f : A → B) × isEquiv A B f;

iso (A B : U) : U := (f : A → B) × isIso A B f;

idEquiv (A : U) : equiv A A :=
  (λ x. x, λ x. x, λ x _. x, λ x _. x, λ x _ _. x);

ua (A B : U)(f : equiv A B) : A = B :=
  λ i. Glue B [i=0. (A, f); i=1. (B, idEquiv B)];

J (A : U) (a : A) (C : (x : A) → a = x → U)
  (d : C a (λ _. a))(x : A) (p : a = x) : C x p
  := let sq (i j : I) : A := hcom 0 j [i=0 _. a; i=1. p] a;
     coe 0 1 (i. C (sq i 1) (λ j. sq i j)) d;

JEq (A : U) (a : A) (C : (x : A) → a = x → U) (d : C a (λ _. a))
  : J A a C d a (λ _. a) = d :=

  let sq (i j : I) : A := hcom 0 j [i=0 _. a; i=1 _. a] a;
  let cube (i j k : I) : A := hcom 0 j [k=0 x. sq i x;
                                        k=1 _. a;
                                        i=0 _. a;
                                        i=1 _. a] a;

  λ k. com 0 1 (i. C (cube i 1 k) (λ j. cube i j k))
               [k=0 i. coe 0 i (i. C (sq i 1) (λ j. sq i j)) d;
                k=1 _. d]
               d;

fiber (A B : U) (f : A → B) (b : B) : U
  := (x : A) * f x = b;

fiberRefl (A B : U) (f : A → B) (a  : A) : fiber A B f (f a)
  := (a , refl);

contractIsoFiber (A B : U) (is : iso A B) (a : A)
  (fib : fiber A B is.f (is.f a))
  : fiberRefl A B is.f a = fib
  :=
  let sq (j k : I) : A :=
      hcom 0 j [k=1 j. is.g (fib.2 j)] (is.linv (fib.1) k);
  let sq2 (i k : I) : A :=
      hcom 1 k
        [i=0 k. is.linv a k;
         i=1 k. sq k k]
        (is.g (is.f a));
  λ i.
  (sq2 i 0,
   λ j.
   let aux : A :=
     hcom j 1
       [i=0 k. is.linv a k;
        i=1 k. sq j k;
        j=0 k. is.linv (sq2 i 0) k]
       (sq2 i j);
   hcom 0 1
     [i=0 l. is.rinv (is.f a) l;
      i=1 l. is.rinv (fib.2 j) l;
      j=0 l. is.rinv (is.f (sq2 i 0)) l;
      j=1 l. is.rinv (is.f a) l]
     (is.f aux));

isoToEquiv (A B : U) (is : iso A B) : equiv A B :=
    is.f
  , is.g
  , λ a i. (contractIsoFiber A B is a (is.g (is.f a), is.rinv (is.f a)) i).1
  , is.rinv
  , λ a i k. (contractIsoFiber A B is a (is.g (is.f a), is.rinv (is.f a)) i).2 k
  ;

--------------------------------------------------------------------------------
